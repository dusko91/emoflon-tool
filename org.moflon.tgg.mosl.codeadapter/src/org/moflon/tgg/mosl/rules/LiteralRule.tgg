#using org.moflon.tgg.mosl.codeadapter.*  #using AttrCondDefLibrary.*

#rule LiteralRule #with org.moflon.tgg.mosl.codeadapter

#source {
	++ literal : LiteralExpression
	attrCond : AttrCond {
		++   - values->literal
	}
	rule : Rule {
		- attrConditions->attrCond
	}
}

#target {
	tggCSP : CSP {
		++   - variables->tggLiteral
		- constraints->tggConstraint
		- variables->literalNAC
	}
	tggRule : TGGRule {
		- csp->tggCSP
	}
	++ tggLiteral : Literal {
		bound := true
	}
	tggConstraint : TGGConstraint {
		++   - variables->tggLiteral
	}
	! literalNAC : Literal {
		value == literal . value
	}
}

#correspondence {
	attrCondToTggConstraint : AttrCondToTGGConstraint {
		#src->attrCond
		#trg->tggConstraint
	}
	++   literalToTggLiteral : ParamValueToVariable {
		#src->literal
		#trg->tggLiteral
	}
	ruleToTggRule : RuleToTGGRule {
		#src->rule
		#trg->tggRule
	}
}

#attributeConditions {
	isNotEnum(tggLiteral.value)
	eq (literal.value, tggLiteral.value)
}

